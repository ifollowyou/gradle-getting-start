buildscript {
    repositories {
        maven {
            url 'http://localhost:8081/nexus/content/groups/public/'
        }

        mavenLocal()
        jcenter()

    }
    dependencies {
        classpath 'org.unbroken-dome.gradle-plugins:gradle-testsets-plugin:1.2.0'
    }
}

apply plugin: 'application'
apply plugin: 'java'
apply plugin: 'org.unbroken-dome.test-sets'

repositories {
    maven {
        url 'http://localhost:8081/nexus/content/groups/public/'
    }

    mavenLocal()
    mavenCentral()
}

testSets {
    integrationTest { dirName = 'integration-test' }
}

dependencies {
    compile 'ch.qos.logback:logback-classic:1.1.3'

    testCompile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-core:3.1.0'
}

//If you want to ensure that integration tests are run every time when you invoke
//this task, uncomment the following block.
project.integrationTest {
    outputs.upToDateWhen { false }
}

//Ensure that the check task fails the build if there are failing integration tests.
check.dependsOn integrationTest
//Ensure that our unit tests are run before our integration tests
integrationTest.mustRunAfter test

//Ensure that the HTML reports of unit and integration tests are written to different directories.
tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

mainClassName = 'net.petrikainulainen.gradle.HelloWorld'

task copyLicense {
    outputs.file new File("$buildDir/LICENSE")
    doLast {
        copy {
            from "LICENSE"
            into "$buildDir"
        }
    }
}

applicationDistribution.from(copyLicense) {
    into ""
}